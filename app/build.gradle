buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name.equals('lint')) {
        task.enabled = false
    }
}


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'me.vickychijwani.spectre'
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1_005_002      // 3 digits each for major, minor, patch
        versionName '1.5.2'

        // enable support for vector drawables on API < 21
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    signingConfigs {
//        release {
//            storeFile(file(System.getenv('KEYSTORE') ?: '') ?: '')
//            storePassword(System.getenv('KEYSTORE_PASSWORD') ?: '')
//            keyAlias(System.getenv('KEY_ALIAS') ?: '')
//            keyPassword(System.getenv('KEY_PASSWORD') ?: '')
//        }
//    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
//            signingConfig signingConfigs.release
            minifyEnabled false   // FIXME disabled because login, revoke token strangely stopped working
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
        }
    }
    lintOptions {
        // check for lint errors but don't abort builds
        // I had to disable it because we don't get translations for new strings immediately
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// Fabric / Crashlytics keys are mandatory for release builds
task checkFabricPropertiesExists << {
    if (!file('fabric.properties').exists()) {
        throw new GradleException('Missing fabric.properties file for Fabric / Crashlytics!')
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn checkFabricPropertiesExists
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // reactive extensions (rx)
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.trello:rxlifecycle:0.1.0'
    compile 'com.trello:rxlifecycle-components:0.1.0'

    // networking / communication / data storage
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.jakewharton.retrofit:retrofit1-okhttp3-client:1.1.0'   // temporary
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'  // temporary
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.6'

    // UI
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:percent:25.1.1'
    compile 'com.android.support:customtabs:25.1.1'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.0.Final'
    compile 'com.tsengvn:typekit:1.0.1'

    // misc
    compile 'com.jakewharton:butterknife:7.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:answers:1.3.7@aar') {
        transitive = true
    }
    compile 'com.github.slugify:slugify:2.1.3'

    // debug
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugCompile 'io.palaima.debugdrawer:debugdrawer:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-commons:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-okhttp3:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-picasso:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-scalpel:0.7.0'

    // test
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.json:json:20140107'    // don't depend on Android's JSONObject: http://stackoverflow.com/a/30759769/504611
}
